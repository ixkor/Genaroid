Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def sdkDir = properties.getProperty('sdk.dir')

repositories {
    maven { url "$sdkDir/extras/android/m2repository/" }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceSets {
    main.java.srcDirs += "$projectDir/../common/src/main/java"
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile files("${System.properties['java.home']}/../lib/tools.jar")
    compile 'com.squareup:javapoet:1.2.0'
    compile 'com.android.support:support-annotations:23.3.0'
    compile 'com.google.auto.service:auto-service:1.0-rc2'
}

javadoc {
    options {
        links "http://docs.oracle.com/javase/7/docs/api/"
        linksOffline "http://d.android.com/reference", "$sdkDir/docs/reference"
    }
    failOnError false
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    if (project.hasProperty("bintrayApiKey")) {
        user = bintrayUserName
        key = bintrayApiKey
    }

    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = 'genaroid:compiler'
        websiteUrl = 'https://github.com/ixkor/genaroid'
        issueTrackerUrl = ''
        vcsUrl = 'https://github.com/ixkor/genaroid.git'
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
    }
}
