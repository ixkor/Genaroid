apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 11
    }

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    sourceSets {
        main.java.srcDirs += "$projectDir/../common/src/main/java"
    }
}

dependencies {
    provided 'com.android.support:support-v4:23.1.1'
    provided 'com.android.support:support-annotations:23.1.1'
    //noinspection GradleDynamicVersion
    apt 'net.xkor.genaroid:compiler:+'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompile.classpath.files
    })
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    exclude '**/BuildConfig.java'
    exclude '**/R.java'

    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options {
        links "http://docs.oracle.com/javase/7/docs/api/"
        linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
    }
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar

            android.libraryVariants.all { variant ->
                def name = variant.buildType.name
                if (name.equals(com.android.builder.core.BuilderConstants.DEBUG)) {
                    return; // Skip debug builds.
                }
                def task = project.tasks.create "jar${name.capitalize()}", Jar
                task.dependsOn variant.javaCompile
                task.from variant.javaCompile.destinationDir
                task.exclude '**/BuildConfig.class'
                artifact task
            }
        }
    }
}

bintray {
    if (project.hasProperty("bintrayApiKey")) {
        user = bintrayUserName
        key = bintrayApiKey
    }

    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = 'genaroid:core'
        websiteUrl = 'https://github.com/ixkor/genaroid'
        issueTrackerUrl = ''
        vcsUrl = 'https://github.com/ixkor/genaroid.git'
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
    }
}
